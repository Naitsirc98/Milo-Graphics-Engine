// https://github.com/TheCherno/Hazel

#version 450 core

// Pre-filters environment cube map using GGX NDF importance sampling.
// Part of specular IBL split-sum approximation.

const float PI = 3.141592;
const float PI2 = 2 * PI;
const float Epsilon = 0.00001;

const int NumMipLevels = 5;
layout(set = 0, binding = 0) uniform samplerCube u_EnvironmentMap;
layout(set = 0, binding = 1, rgba16f) restrict writeonly uniform imageCube u_PrefilterMap[NumMipLevels];

layout (push_constant) uniform PushConstants {
	float u_Roughness;
	int u_EnvironmentMapResolution;
	int u_MipLevel;
	uint u_NumSamples;
};

vec3 GetCubeMapTexCoord() {
	vec2 st = gl_GlobalInvocationID.xy / vec2(imageSize(u_PrefilterMap[u_MipLevel]));
	vec2 uv = 2.0 * vec2(st.x, 1.0 - st.y) - vec2(1.0);

	vec3 ret;
	if (gl_GlobalInvocationID.z == 0)      ret = vec3(  1.0, uv.y, -uv.x);
	else if (gl_GlobalInvocationID.z == 1) ret = vec3( -1.0, uv.y,  uv.x);
	else if (gl_GlobalInvocationID.z == 2) ret = vec3( uv.x,  1.0, -uv.y);
	else if (gl_GlobalInvocationID.z == 3) ret = vec3( uv.x, -1.0,  uv.y);
	else if (gl_GlobalInvocationID.z == 4) ret = vec3( uv.x, uv.y,   1.0);
	else if (gl_GlobalInvocationID.z == 5) ret = vec3(-uv.x, uv.y,  -1.0);
	return normalize(ret);
}

float distributionGGX(vec3 N, vec3 H, float roughness)
{
	float a = roughness * roughness;
	float a2 = a * a;
	float NdotH = max(dot(N, H), 0.0);
	float NdotH2 = NdotH * NdotH;

	float nom   = a2;
	float denom = (NdotH2 * (a2 - 1.0) + 1.0);
	denom = PI * denom * denom;

	return nom / denom;
}
// ----------------------------------------------------------------------------
// http://holger.dammertz.org/stuff/notes_HammersleyOnHemisphere.html
// efficient VanDerCorpus calculation.
float radicalInverse_VdC(uint bits)
{
	bits = (bits << 16u) | (bits >> 16u);
	bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
	bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
	bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
	bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
	return float(bits) * 2.3283064365386963e-10; // / 0x100000000
}
// ----------------------------------------------------------------------------
vec2 hammersley(uint i, uint N)
{
	return vec2(float(i)/float(N), radicalInverse_VdC(i));
}
// ----------------------------------------------------------------------------
vec3 importanceSampleGGX(vec2 Xi, vec3 N, float u_Roughness)
{
	float a = u_Roughness*u_Roughness;

	float phi = 2.0 * PI * Xi.x;
	float cosTheta = sqrt((1.0 - Xi.y) / (1.0 + (a*a - 1.0) * Xi.y));
	float sinTheta = sqrt(1.0 - cosTheta*cosTheta);

	// from spherical coordinates to cartesian coordinates - halfway vector
	vec3 H;
	H.x = cos(phi) * sinTheta;
	H.y = sin(phi) * sinTheta;
	H.z = cosTheta;

	// from tangent-space H vector to world-space sample vector
	vec3 up          = abs(N.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(1.0, 0.0, 0.0);
	vec3 tangent   = normalize(cross(up, N));
	vec3 bitangent = cross(N, tangent);

	vec3 sampleVec = tangent * H.x + bitangent * H.y + N * H.z;
	return normalize(sampleVec);
}


vec3 calculatePrefilteredColor(vec3 N, vec3 R, vec3 V) {

	vec3 prefilteredColor = vec3(0.0);
	float totalWeight = 0.0;

	const float InvNumSamples = 1.0 / float(u_NumSamples);

	for(uint i = 0u; i < u_NumSamples; ++i) {
		// generates a sample vector that's biased towards the preferred alignment direction (importance sampling).
		vec2 Xi = hammersley(i, u_NumSamples);
		vec3 H = importanceSampleGGX(Xi, N, u_Roughness);
		vec3 L = normalize(2.0 * dot(V, H) * H - V);

		float NdotL = max(dot(N, L), 0.0);

		if(NdotL > 0.0) {
			// sample from the environment's mip level based on u_Roughness/pdf
			float D = distributionGGX(N, H, u_Roughness);
			float NdotH = max(dot(N, H), 0.0);
			float HdotV = max(dot(H, V), 0.0);
			float pdf = D * NdotH / (4.0 * HdotV) + 0.0001;

			float saTexel  = 4.0 * PI / (6.0 * u_EnvironmentMapResolution * u_EnvironmentMapResolution);
			float saSample = InvNumSamples * pdf + 0.001;

			float mipLevel = u_Roughness == 0.0 ? 0.0 : 0.5 * log2(saSample / saTexel);

			prefilteredColor += textureLod(u_EnvironmentMap, L, mipLevel).rgb * NdotL;

			totalWeight += NdotL;
		}
	}

	prefilteredColor = prefilteredColor / totalWeight;

	return prefilteredColor;
}

layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;
void main(void) {
	// Make sure we won't write past output when computing higher mipmap levels.
	ivec2 outputSize = imageSize(u_PrefilterMap[u_MipLevel]);
	if(gl_GlobalInvocationID.x >= outputSize.x || gl_GlobalInvocationID.y >= outputSize.y) {
		return;
	}

	vec3 N = GetCubeMapTexCoord();

	// Make the simplyfying assumption that V equals R equals the normal.
	vec3 R = N;
	vec3 V = R;

	vec3 prefilteredColor = calculatePrefilteredColor(N, R, V);

	imageStore(u_PrefilterMap[u_MipLevel], ivec3(gl_GlobalInvocationID), vec4(prefilteredColor, 1.0));
}